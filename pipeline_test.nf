#!/usr/bin/env nextflow
nextflow.enable.dsl=2

def helpMessage() {
log.info """
        Usage:
        The options for running the pipeline to build models are structured as follows:
        --option                        Description/Notes [default value]

    Mandatory inputs:
        --assemblypath                  Directory of your fasta files (full path required) [input_test]
        
        --pv_fasta                      Fasta file of the pv used to train the pv model (generated by previous pipeline) [out/4.model/model_ref/pv_filter.fasta ]
        --igr_fasta                     Fasta file of the igr sequences used to train the igr model (generated by previous pipeline) [out/4.model/model_ref/igr_filter.fasta ]
        --models                        Folder of where your models are stored (generated by previous pipeline) [out/4.model/models]
        --snp_core_ref                  Reference file of your core snps (generated by previous pipeline) [out/2.genomic_features/snp_core_out/core.ref.tab]

    Optional arguments:
        --outdir                        Output directory for models & other data [./out]
    
    Hostdata file options:
        --assembly_column               Column name of your assemblies. Must contain extension eg myassembly.fasta ["Filename"]

    Tool-specific arguments:
        --prokka_ref                    'Trusted' protein file for prokka (prokka --proteins) [./data/stm_proteinref.fasta]
        --prokka_extra                  Additional options for prokka anootation, eg."--genus Enterococcus" []
        --amr_species                   Assembly species for amrfinder (amrfinder -O) ["Salmonella"]
        --snp_ref                       Reference file for snippy (snippy --ref) [/data/stm_sl1344.fasta]

    Nextflow Options
        profile                         Run with docker or singularity (options are docker or singularity)
        """
}

// Show help message
if (params.help) {
    helpMessage()
    exit 0
}

// Modules 
include { assembly_qc                           } from "$projectDir/scripts/modules/assembly_qc.nf"
include { printqc                               } from "$projectDir/scripts/modules/printqc.nf"
include { prokka_annotation                     } from "$projectDir/scripts/modules/prokka_annotation.nf"
include { amrfinder                             } from "$projectDir/scripts/modules/amrfinder.nf"
include { snippy                                } from "$projectDir/scripts/modules/snippy.nf"
include { snippy_core                           } from "$projectDir/scripts/modules/snippy_core.nf"
include { amr_collect                           } from "$projectDir/scripts/modules/amr_collect.nf"  
include { get_igr_fastas                        } from "$projectDir/scripts/modules/get_igr_fastas.nf"
include { get_pv_fastas                         } from "$projectDir/scripts/modules/get_pv_fastas.nf"
include { makeblastdb                           } from "$projectDir/scripts/modules/makeblastdb.nf"
include { blast as blast_pvs                    } from "$projectDir/scripts/modules/blast.nf"
include { blast as blast_igrs                   } from "$projectDir/scripts/modules/blast.nf"
include { amr_process_test                      } from "$projectDir/scripts/modules/amr_process_test.nf"
include { snp_process_test                      } from "$projectDir/scripts/modules/snp_process_test.nf"
include { blast_process as blast_process_pv     } from "$projectDir/scripts/modules/blast_process.nf"
include { blast_process as blast_process_igr    } from "$projectDir/scripts/modules/blast_process.nf"
include { prediction_testing_models             } from "$projectDir/scripts/modules/prediction_testing_models"

// R script & reference files
amr_process_script = file("$projectDir/scripts/R_scripts/input_amr_testing.R")
snps_process_script = file("$projectDir/scripts/R_scripts/input_snps_testing.R")
blast_process_script = file("$projectDir/scripts/R_scripts/blast_process.R")
prediction_testing_models_script = file ("$projectDir/scripts/R_scripts/prediction_testing_models.R")

pv_fasta_file = file(params.pv_fasta)
igr_fasta_file = file(params.igr_fasta)
models_dir = file(params.models)
prokka_ref_file = file(params.prokka_ref)
snp_ref_file = file(params.snp_ref)
snp_core_ref_file = file(params.snp_core_ref)

///////////////////////////////     WORKFLOW    ////////////////////////////////////////////////////////////////////////////////////////////
assemblies = Channel.fromPath("${params.assemblypath}/*.${params.fileextension}")

workflow {
    prokka_annotation(assemblies, prokka_ref_file)
    amrfinder(prokka_annotation.out.annotation, prokka_annotation.out.transl_protein, prokka_annotation.out.nucleotide)
    amr_collect(amrfinder.out.collect())
    amr_process_test(amr_process_script, amr_collect.out) 

    snippy(assemblies, snp_ref_file)
    snippy_core(snippy.out.collect(), snp_ref_file)
    snp_process_test(snps_process_script, snippy_core.out.core_snps, snp_core_ref_file)

    makeblastdb(assemblies.collect())
    blast_pvs(makeblastdb.out.blastdb_dir, pv_fasta_file)
    blast_igrs(makeblastdb.out.blastdb_dir, igr_fasta_file)
    blast_process_pv(blast_process_script, blast_pvs.out.blast_results, "pv")
    blast_process_igr(blast_process_script, blast_igrs.out.blast_results, "igr" )

    prediction_testing_models(
        amr_process_test.out.amr_class_all,
        amr_process_test.out.amr_gene_all,
        blast_process_pv.out,
        blast_process_igr.out,
        snp_process_test.out,
        models_dir,
        prediction_testing_models_script
    )
}
